plugins {
    id 'com.android.application'
    id 'com.google.protobuf' version '0.8.18'
}

android {
    compileSdk 31

    defaultConfig {
        applicationId "com.example.app"
        minSdk 26
        targetSdk 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        disable 'GoogleAppIndexingWarning', 'HardcodedText', 'InvalidPackage'
        textReport true
        textOutput "stdout"
    }
}

def protobufVersion = '3.19.1'
def grpcVersion = '1.43.1'

dependencies {
    // https://mvnrepository.com/artifact/io.grpc/grpc-okhttp
    implementation "io.grpc:grpc-okhttp:${grpcVersion}"
    // https://mvnrepository.com/artifact/io.grpc/grpc-protobuf-lite
    implementation "io.grpc:grpc-protobuf-lite:${grpcVersion}"
    // https://mvnrepository.com/artifact/io.grpc/grpc-stub
    implementation "io.grpc:grpc-stub:${grpcVersion}"

    compileOnly 'org.apache.tomcat:annotations-api:6.0.53'
    implementation 'javax.annotation:javax.annotation-api:1.3.2'

    implementation 'androidx.appcompat:appcompat:1.4.0'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.2'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${protobufVersion}"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java { option 'lite' }
            }
            task.plugins {
                grpc { option 'lite' }
            }
        }
    }
}